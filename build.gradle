plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'com.example'
version = '1.0.0'
description = 'Order Tracking Common'

java {
    toolchain { languageVersion = JavaLanguageVersion.of(17) }
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
}

ext {
    jacksonVersion = '2.17.2'
    lombokVersion  = '1.18.32'
    junitVersion   = '5.10.3'
}

dependencies {
    api "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
}

tasks.test { useJUnitPlatform() }

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'order-tracking-common'
                description = 'Shared models/DTOs/events for Order Tracking microservices'
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/arunsingh8775/order-tracking-common")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
